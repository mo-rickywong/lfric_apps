!-----------------------------------------------------------------------------
! (C) Crown copyright 2024 Met Office. All rights reserved.
! For further details please refer to the file COPYRIGHT.txt
! which you should have received as part of this distribution.
!-----------------------------------------------------------------------------

!> Test the mixed operator of the semi-implicit lhs
module apply_mixed_operator_kernel_mod_test

  use constants_mod, only : i_def, r_solver
  use funit

  implicit none

  private
  public :: test_all

contains

  @test( )
  subroutine test_all( )

    use apply_mixed_operator_kernel_mod, only : apply_mixed_operator_code

    implicit none

    real(r_solver), parameter :: tol = 1.0e-12_r_solver

    ! Mesh
    integer(i_def), parameter :: nlayers = 1
    integer(i_def), parameter :: cell = 1
    integer(i_def), parameter :: ncell = 1

    ! Spaces
    integer(i_def), parameter :: ndf_w2h = 2
    integer(i_def), parameter :: ndf_w2v = 2
    integer(i_def), parameter :: ndf_w2 = ndf_w2h+ndf_w2v
    integer(i_def), parameter :: ndf_wt = 2
    integer(i_def), parameter :: ndf_w3 = 1
    integer(i_def), parameter :: undf_w2h = ndf_w2h*nlayers
    integer(i_def), parameter :: undf_w2v = nlayers + 1
    integer(i_def), parameter :: undf_w2 = undf_w2h + undf_w2v
    integer(i_def), parameter :: undf_wt = nlayers + 1
    integer(i_def), parameter :: undf_w3 = ndf_w3*nlayers

    ! Maps
    integer(i_def), dimension(ndf_w2h) :: map_w2h
    integer(i_def), dimension(ndf_w2v) :: map_w2v
    integer(i_def), dimension(ndf_w2)  :: map_w2
    integer(i_def), dimension(ndf_wt)  :: map_wt
    integer(i_def), dimension(ndf_w3)  :: map_w3

    ! Operators
    real(r_solver), dimension(ndf_w2, ndf_w2, ncell) :: Mu
    real(r_solver), dimension(ndf_w2, ndf_wt, ncell) :: P2t
    real(r_solver), dimension(ndf_w2, ndf_w3, ncell) :: Grad
    real(r_solver), dimension(ndf_wt, ndf_w2, ncell) :: Pt2
    real(r_solver), dimension(ndf_w3, ndf_w3, ncell) :: M3p
    real(r_solver), dimension(ndf_w3, ndf_wt, ncell) :: P3t
    real(r_solver), dimension(ndf_w3, ndf_w2, ncell) :: Q32

    ! Fields
    real(r_solver), dimension(undf_w2)  :: Nu, u
    real(r_solver), dimension(undf_w2h) :: Luv, uv
    real(r_solver), dimension(undf_w2v) :: Lw, w
    real(r_solver), dimension(undf_wt)  :: invMt
    real(r_solver), dimension(undf_w3)  :: p, Lp
    real(r_solver), dimension(undf_wt)  :: t

    integer(i_def) :: df
    real(r_solver) :: answer

    map_w2h = (/ 1_i_def, 1_i_def + nlayers /)
    map_w2v = (/ 1_i_def, 2_i_def /)
    map_w2 = (/ 1_i_def, 1_i_def + nlayers, 1_i_def + 2*nlayers, 2_i_def + 2*nlayers /)
    map_wt = (/ 1_i_def, 2_i_def /)
    map_w3 = (/ 1_i_def /)

    ! Set up operators
    Mu = 0.0_r_solver
    do df = 1,ndf_w2
      Mu(df,df,1) = real(df,r_solver)
      Grad(df,1,1) = (-1.0_r_solver)**df
    end do

    P2t(:,:,1) = 0.0_r_solver
    P2t(3:4,:,1) = 0.5_r_solver
    Pt2 = 0.0_r_solver
    Pt2(1,3,1) = 7.0_r_solver
    Pt2(2,4,1) = 9.0_r_solver
    M3p = 0.5_r_solver
    Q32 = 3.0_r_solver
    P3t = 5.0_r_solver

    ! Set up the fields
    uv = (/ 0.0_r_solver, 1.0_r_solver /)
    w = (/ 3.0_r_solver, -2.0_r_solver /)
    p = (/ 1.2_r_solver /)
    Nu = 2.0_r_solver
    invMt = 0.25_r_solver
    Luv = 0.0_r_solver
    Lw = 0.0_r_solver

    ! Compute: (L_uv, L_w) =  Nu*(Mu*u - Proj*t - Grad*p)
    !          L_p   = M3p*p - P3t*t + Q32*u
    ! with t = -Mt^-1 * Pt2*u
    call apply_mixed_operator_code(cell,                       &
                                   nlayers,                    &
                                   Luv, Lw, Lp,                &
                                   uv, w, p,                   &
                                   ncell, Pt2,                 &
                                   invMt,                      &
                                   ncell, Mu,                  &
                                   ncell, P2t,                 &
                                   ncell, Grad,                &
                                   Nu,                         &
                                   ncell, M3p,                 &
                                   ncell, Q32,                 &
                                   ncell, P3t,                 &
                                   ndf_w2h, undf_w2h, map_w2h, &
                                   ndf_w2v, undf_w2v, map_w2v, &
                                   ndf_w3, undf_w3, map_w3,    &
                                   ndf_wt, undf_wt, map_wt,    &
                                   ndf_w2, undf_w2, map_w2)
    t(:) = 0.0_r_solver
    do df = 1, nlayers
      t(df) = t(df) - invMt(df)*Pt2(1,3,1)*w(df)
      t(df+1) = - invMt(df)*Pt2(2,4,1)*w(df+1)
    end do

    do df = 1,ndf_w2h
      answer = Nu(df)*(Mu(df,df,1)*uv(df) - sum(P2t(df,:,1)*t(:)) - Grad(df,1,1)*p(1))
      @assertEqual(answer, Luv(df), tol)
    end do

    do df = 1,ndf_w2v
      @assertEqual(0.0_r_solver, Lw(df), tol)
    end do

    u = (/ uv, w /)
    answer = M3p(1,1,1)*p(1) - sum(P3t(1,:,1)*t(:)) + sum(Q32(1,:,1)*u(:))
    @assertEqual(answer, Lp(1), tol)

  end subroutine test_all

end module apply_mixed_operator_kernel_mod_test
